/*
 * Copyright (c) 2014 Swinburne University of Technology
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */


apply plugin: 'java'
apply plugin: 'war'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
  mavenCentral()
  maven {
    url "https://bbprepo.blackboard.com/content/repositories/releases/"
  }
}

war.baseName = "oslt-autosignon"

if (isBuildingBlock()) {
  version = getB2Version()
}

configurations {
  b2deploy
}

dependencies {
  b2deploy 'org.oscelot:b2deploy-task:0.1.0'

  compile 'org.slf4j:slf4j-api:1.7.5'
  compile 'org.slf4j:slf4j-log4j12:1.7.5'

  providedCompile 'blackboard.platform:bb-platform:9.1.100401.0'
  providedCompile 'blackboard.platform:bb-cms-admin:9.1.100401.0'
  providedCompile 'blackboard.platform:bb-taglibs:9.1.100401.0'
  providedCompile 'jstl:jstl:1.2'
  providedCompile 'javax.servlet:servlet-api:2.5'


  compile project(':autosignon-api')
  compile files('../lib/bb-stripes-utils-1.0.jar')
  compile 'net.sourceforge.stripes:stripes:1.5.7'
  compile 'org.springframework:spring-beans:4.3.0.RELEASE'
  compile 'org.springframework:spring-web:4.3.0.RELEASE'

  testCompile 'org.mockito:mockito-all:1.9.5'
  testCompile "junit:junit:4.11"
}

ant.taskdef(name: 'b2deploy', classname: 'org.oscelot.ant.B2DeployTask', classpath: configurations.b2deploy.asPath)

task deployb2(dependsOn: 'war') {
  doLast {
    println "Deploying \"" + war.archivePath + "\""
    ant.b2deploy(localfilepath: war.archivePath,
            host: 'localhost:9876',
            clean: 'true',
            courseorgavailable: 'true'
    )
  }
}

String getB2Version() {
  File mfFile = new File(file(webAppDir), 'WEB-INF/bb-manifest.xml');
  def manifest = new XmlSlurper().parse(mfFile);
  return manifest.plugin.version['@value'];
}

boolean isBuildingBlock() {
  File mfFile = new File(file(webAppDir), 'WEB-INF/bb-manifest.xml');
  return mfFile.exists();
}